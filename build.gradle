plugins {
	id 'org.springframework.boot' version '2.7.3'
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
	id 'java'
	id "com.github.node-gradle.node" version "3.4.0"

}

group = 'com.ocado'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

node {
	version = '16.17.0'
	download = true
	workDir = file("${project.projectDir})/src/main/frontend/nodejs")
	npmWorkDir = file("${project.projectDir})/src/main/frontend/npm")
	nodeModulesDir = file("${project.projectDir})/src/main/frontend")
}

task npmInstallDepend(type: NpmTask) {
	dependsOn 'npmSetup'
	execOverrides {
		it.ignoreExitValue = true
		it.workingDir = 'src/main/frontend'
	}
	args = ['install']
}

task npmBuild(type: NpmTask) {
	dependsOn 'npmInstallDepend'
	execOverrides {
		it.workingDir = 'src/main/frontend'
	}
	args = ['run','build']
}

task copyFrontendToBuild(type: Copy) {
	dependsOn 'npmBuild'
	from "$projectDir/src/main/frontend/build/"
	into "$buildDir/resources/main/static"
}

processResources {
	dependsOn 'copyFrontendToBuild'
}
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

tasks.named('test') {
	useJUnitPlatform()
}
